line: [label] [op_code | pseudo_op | variable | region | macro ]  (NEWLINE|ENDMARKER)

label: NAME [ ':' | '?']
variable: ('=' | EQU) exp
region: SEGMENT [PARA | PAGE | exp] | ENDS
macro: '.' NAME ARG (',' ARG)* | MACRO | ENDM

op_code: (group1 | group2  | group3 | group4 | group5 | group6  | group7 | group8 |
          group9 | group10 | group11 | group12 | group13 | group14 | group15 | group16)

group1: GROUP1_INST (imm_exp | [LONG | DP | ZP] exp [xys_index] | long_ind_exp)
group2: GROUP2_INST ([AREG | [DP | ZP] exp [x_index]])
group3: GROUP3_INST ([LONG | DP | ZP] exp)
group4: GROUP4_INST (imm_exp | [DP | ZP] exp [x_index])
group5: GROUP5_INST (imm_exp | [DP | ZP] exp)
group6: GROUP6_INST imm_exp
group7: GROUP7_INST exp ',' exp
group8: GROUP8_INST ([LONG] exp | jmp_long_ind_exp)
group9: GROUP9_INST ([LONG] exp | jmp_long_ind_exp)
group10: GROUP10_INST [LONG] exp
group11: GROUP11_INST (imm_exp | [DP | ZP] exp [y_index])
group12: GROUP12_INST [DP | ZP] exp [y_index]
group13: GROUP13_INST [DP | ZP] exp [x_index]
group14: GROUP14_INST
group15: GROUP15_INST exp
group16: GROUP16_INST [['#'] exp]

imm_exp: [WIDE | SHORT] '#' exp
long_ind_exp: '[' exp ']' [y_index]
jmp_long_ind_exp: '[' exp ']'

x_index: ',' XREG
y_index: ',' YREG
s_index: ',' SREG
xy_index: ',' (XREG | YREG)
xs_index: ',' (XREG | SREG)
ys_index: ',' (YREG | SREG)
xys_index: ',' (XREG | YREG | SREG)

symbol: NAME [(':' | '&') | '.' [NAME]] | '.' NAME

exp: and_cond ('||' and_cond)*
and_cond: not_cond ('&&' not_cond)*
not_cond: '!' not_cond | comparison
comparison: or_exp (comp_op or_exp)*
comp_op: '<' | '>' | '==' | '!=' | '>=' | '<='
or_exp: xor_expr ('|' xor_expr)*
xor_expr: and_expr ('^' and_expr)*
and_expr: shift_expr ('&' shift_expr)*
shift_expr: arith_expr (('<<' | '>>') arith_expr)*
arith_expr: term (('+' | '-') term)*
term: factor (('*' | '/' | '%') factor)*
factor: ('+' | '-' | '~' | '<' | '>' | '>>' | '<<') factor | atom
atom: ('(' exp [xs_index] ')') | STRING | NUMBER | symbol | '*'

pseudo_op: (CPU6502 | CPU65C02 | CPU65C816 | WIDEA | SHORTA | WIDEXY | SHORTXY | define_byte | define_word |
            define_storage | align | block | END_BLOCK | if_def | iff | ELSE | ENDIF )

define_byte: DB exp (',' exp)*
define_word: (DW | DBW | DA) exp (',' exp)*
define_storage: DS exp
align: ALIGN (PARA | PAGE)
block: BLOCK exp

if_def: (IFDEF | IFNDEF) NAME
iff: IF exp



